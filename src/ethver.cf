comment "//" ;
comment "/*" "*/" ;

Prog.     Program  ::= Contract Scenario ;


-- CONTRACT

Contr.    Contract ::= "contract" Ident "{" [Decl] [Function] "}" ;

terminator Decl ";" ;
terminator Function "" ;

Dec.  Decl ::= Type Ident ;

Fun.      Function ::= "function" Ident "(" [Arg] ")" "{" [Stm] "}" ;
FunR.     Function ::= "function" Ident "(" [Arg] ")" "returns" "(" Type ")" 
                       "{" [Stm] "}" ;

Ar.   Arg  ::= Type Ident ;

separator  Arg "," ;

SExp.    Stm ::= Exp ";" ;
SBlock.  Stm ::= "{" [Stm] "}" ;
SIf.     Stm ::= "if" "(" Exp ")" Stm ;
SIfElse. Stm ::= "if" "(" Exp ")" Stm "else" Stm ;
-- SWhile.  Stm ::= "while" "(" Exp ")" Stm ;
SReturn. Stm ::= "return" Exp  ";" ;

terminator Stm "" ;

EAss.    Exp  ::= Ident "=" Exp ;
ELt.     Exp1 ::= Exp2 "<" Exp2 ;
ELe.     Exp1 ::= Exp2 "<=" Exp2 ;
EGt.     Exp1 ::= Exp2 ">" Exp2 ;
EGe.     Exp1 ::= Exp2 ">=" Exp2 ;
EEq.     Exp1 ::= Exp2 "==" Exp2 ;
ENe.     Exp1 ::= Exp2 "!=" Exp2 ;
EAdd.    Exp2 ::= Exp2 "+" Exp3 ;
ESub.    Exp2 ::= Exp2 "-" Exp3 ;
EMul.    Exp3 ::= Exp3 "*" Exp4 ;
EDiv.    Exp3 ::= Exp3 "/" Exp4 ;
EMod.    Exp3 ::= Exp3 "%" Exp4 ;
ECall.   Exp4 ::= [Ident] "(" [CallArg] ")" ;
ESend.   Exp4 ::= Exp "." "send" "(" [CallArg] ")" ;
EVar.    Exp4 ::= Ident ;
EValue.  Exp4 ::= "msg" "." "value" ;
ESender. Exp4 ::= "msg" "." "sender" ;
EStr.    Exp4 ::= String ;
EInt.    Exp4 ::= Integer ;

coercions Exp 4 ;
separator CallArg "," ;
separator nonempty Ident "." ;

AExp.   CallArg ::= Exp ;
ABra.   CallArg ::= "{" "from" ":" Exp "," "value" ":" Exp "}" ;

TUInt.   Type ::= "uint" "(" Integer ")" ;
-- TRInt.   Type ::= "random_int" ;
-- TRStr.   Type ::= "random_string" ;


-- SCENARIO

Scen.  Scenario ::= [Decl] [Stm] ;