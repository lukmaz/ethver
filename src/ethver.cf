comment "//" ;
comment "/*" "*/" ;

Prog.     Program  ::= Contract [Scenario] ;
terminator Scenario "" ;

-- CONTRACT

Contr.    Contract ::= "contract" Ident "{" [UserDecl] [Decl] [Function] "}" ;

-- SCENARIO

Scen.  Scenario ::= "scenario" Ident "{" [Decl] [Stm] "}" ;

terminator UserDecl ";" ;
terminator Decl ";" ;
terminator Function "" ;

UDec. UserDecl ::= "user" String ;
Dec.  Decl     ::= Type Ident ;

Fun.      Function ::= "function" Ident "(" [Arg] ")" "{" [Stm] "}" ;
FunV.     Function ::= "function_with_value" Ident "(" [Arg] ")" "{" [Stm] "}" ;
FunR.     Function ::= "function" Ident "(" [Arg] ")" "returns" "(" Type ")" 
                       "{" [Stm] "}" ;

Ar.   Arg  ::= Type Ident ;

separator  Arg "," ;

SExp.    Stm ::= Exp ";" ;
SBlock.  Stm ::= "{" [Stm] "}" ;
SIf.     Stm ::= "if" "(" Exp ")" Stm ;
SIfElse. Stm ::= "if" "(" Exp ")" Stm "else" Stm ;
-- SWhile.  Stm ::= "while" "(" Exp ")" Stm ;
SReturn. Stm ::= "return" Exp  ";" ;

terminator Stm "" ;

EAss.    Exp  ::= Ident "=" Exp ;
EOr.     Exp1 ::= Exp1 "||" Exp2 ;
EAnd.    Exp2 ::= Exp2 "&&" Exp3 ;
EEq.     Exp3 ::= Exp3 "==" Exp4 ;
ENe.     Exp3 ::= Exp3 "!=" Exp4 ;
ELt.     Exp4 ::= Exp4 "<" Exp5 ;
ELe.     Exp4 ::= Exp4 "<=" Exp5 ;
EGt.     Exp4 ::= Exp4 ">" Exp5 ;
EGe.     Exp4 ::= Exp4 ">=" Exp5 ;
EAdd.    Exp5 ::= Exp5 "+" Exp6 ;
ESub.    Exp5 ::= Exp5 "-" Exp6 ;
EMul.    Exp6 ::= Exp6 "*" Exp7 ;
EDiv.    Exp6 ::= Exp6 "/" Exp7 ;
EMod.    Exp6 ::= Exp6 "%" Exp7 ;
ENeg.    Exp7 ::= "-" Exp8 ;
ENot.    Exp7 ::= "!" Exp8 ;
ECall.   Exp8 ::= [Ident] "(" [CallArg] ")" ;
ESend.   Exp8 ::= Exp "." "send" "(" [CallArg] ")" ;
EVar.    Exp8 ::= Ident ;
EValue.  Exp8 ::= "msg" "." "value" ;
ESender. Exp8 ::= "msg" "." "sender" ;
EStr.    Exp8 ::= String ;
EInt.    Exp8 ::= Integer ;
ETrue.   Exp8 ::= "true" ;
EFalse.  Exp8 ::= "false" ;

coercions Exp 8 ;
separator CallArg "," ;
separator nonempty Ident "." ;

AExp.   CallArg ::= Exp ;
ABra.   CallArg ::= "{" "from" ":" Exp "," "value" ":" Exp "}" ;
AWait.  CallArg ::= "{" "from" ":" Exp "," "value" ":" Exp "," "wait" ":" Exp "}" ;

TUInt.   Type ::= "uint" "(" Integer ")" ;
TBool.   Type ::= "bool" ;
TRUInt.   Type ::= "random_uint" "(" Integer ")" ;
-- TRStr.   Type ::= "random_string" ;


