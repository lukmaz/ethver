comment "//" ;
comment "/*" "*/" ;

Prog.     Program  ::= [UserDecl] [ConstantDecl] Contract Communication [Scenario] ;

terminator UserDecl ";" ;
terminator ConstantDecl ";" ;
terminator Scenario "" ;

-- USERS

UDec.   UserDecl ::= "user" String ;

-- CONSTANTS

Const.  ConstantDecl ::= Ident "=" Integer ;

-- CONTRACT

Contr.    Contract ::= "contract" Ident "{" [Decl] [Function] "}" ;
Dec.      Decl     ::= Type Ident ;
ArrDec.   Decl     ::= Type Ident "[" Integer "]" ;

terminator Decl ";" ;
terminator Function "" ;

-- COMMUNICATION

Comm.  Communication ::= "communication" "{" [Decl] [Function] "}" ;

-- SCENARIO

Scen.  Scenario ::= "scenario" Ident "{" [Decl] [Stm] "}" ;

Fun.      Function ::= "function" Ident "(" [Arg] ")" "{" [Stm] "}" ;
FunV.     Function ::= "function_with_value" Ident "(" [Arg] ")" "{" [Stm] "}" ;
FunR.     Function ::= "function" Ident "(" [Arg] ")" "returns" "(" Type ")" 
                       "{" [Stm] "}" ;

Ar.   Arg  ::= Type Ident ;

separator  Arg "," ;

-- SExp.    Stm ::= Exp ";" ;
SBlock.  Stm ::= "{" [Stm] "}" ;
SAsses.  Stm ::= [Ass] ";" ;
SIf.     Stm ::= "if" "(" Exp ")" Stm ;
SIfElse. Stm ::= "if" "(" Exp ")" Stm "else" Stm ;
SWhile.  Stm ::= "while" "(" Exp ")" Stm ;
SReturn. Stm ::= "return" Exp  ";" ;
SSend.   Stm ::= Exp "." "send" "(" Exp ")" ";" ;
SSendT.  Stm ::= Ident "." "sendTransaction" "(" [CallArg] ")" ";" ;
SSendC.  Stm ::= Ident "." "sendCommunication" "(" [Exp] ")" ";" ;
SWait.   Stm ::= "wait" "(" Exp ")" ";" ;

terminator Stm "" ;
separator nonempty Ass "," ;
separator Exp "," ;

AAss.    Ass ::= Ident "=" Exp ;
AArrAss. Ass ::= Ident "[" Exp "]" "=" Exp ;

EOr.     Exp ::= Exp "||" Exp1 ;
EAnd.    Exp1 ::= Exp1 "&&" Exp2 ;
EEq.     Exp2 ::= Exp2 "==" Exp3 ;
ENe.     Exp2 ::= Exp2 "!=" Exp3 ;
ELt.     Exp3 ::= Exp3 "<" Exp4 ;
ELe.     Exp3 ::= Exp3 "<=" Exp4 ;
EGt.     Exp3 ::= Exp3 ">" Exp4 ;
EGe.     Exp3 ::= Exp3 ">=" Exp4 ;
EAdd.    Exp4 ::= Exp4 "+" Exp5 ;
ESub.    Exp4 ::= Exp4 "-" Exp5 ;
EMul.    Exp5 ::= Exp5 "*" Exp6 ;
EDiv.    Exp5 ::= Exp5 "/" Exp6 ;
EMod.    Exp5 ::= Exp5 "%" Exp6 ;
ENeg.    Exp6 ::= "-" Exp7 ;
ENot.    Exp6 ::= "!" Exp7 ;
EArray.  Exp7 ::= Ident "[" Exp "]" ;
ECall.   Exp7 ::= Ident "." "call" "(" [Exp] ")" ;
ERand.   Exp7 ::= "random" "(" Exp ")" ;
ERandL.  Exp7 ::= "random_lazy" "(" Exp ")" ;
EVar.    Exp7 ::= Ident ;
EValue.  Exp7 ::= "msg" "." "value" ;
ESender. Exp7 ::= "msg" "." "sender" ;
EStr.    Exp7 ::= String ;
EInt.    Exp7 ::= Integer ;
ETrue.   Exp7 ::= "true" ;
EFalse.  Exp7 ::= "false" ;

_. Exp   ::= Exp1 ;
_. Exp1  ::= Exp2 ;
_. Exp2  ::= Exp3 ;
_. Exp3  ::= Exp4 ;
_. Exp4  ::= Exp5 ;
_. Exp5  ::= Exp6 ;
_. Exp6  ::= Exp7 ;
_. Exp7  ::= Exp8 ;

coercions Exp 8 ;
separator CallArg "," ;
separator nonempty Ident "." ;

AExp.   CallArg ::= Exp ;
ABra.   CallArg ::= "{" "from" ":" Exp "," "value" ":" Exp "}" ;

TUInt.   Type ::= "uint" "(" Integer ")" ;
TBool.   Type ::= "bool" ;
TRUInt.   Type ::= "random_uint" "(" Integer ")" ;
-- TRStr.   Type ::= "random_string" ;


