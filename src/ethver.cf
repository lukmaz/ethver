comment "//" ;
comment "/*" "*/" ;

Prog.     Program  ::= Contract [Scenario] ;
terminator Scenario "" ;

-- CONTRACT

Contr.    Contract ::= "contract" Ident "{" [UserDecl] [Decl] [Function] "}" ;

-- SCENARIO

Scen.  Scenario ::= "scenario" Ident "{" [Decl] [Stm] "}" ;

terminator UserDecl ";" ;
terminator Decl ";" ;
terminator Function "" ;

UDec. UserDecl ::= "user" String ;
Dec.  Decl     ::= Type Ident ;

Fun.      Function ::= "function" Ident "(" [Arg] ")" "{" [Stm] "}" ;
FunR.     Function ::= "function" Ident "(" [Arg] ")" "returns" "(" Type ")" 
                       "{" [Stm] "}" ;

Ar.   Arg  ::= Type Ident ;

separator  Arg "," ;

SExp.    Stm ::= Exp ";" ;
SBlock.  Stm ::= "{" [Stm] "}" ;
SIf.     Stm ::= "if" "(" Exp ")" Stm ;
SIfElse. Stm ::= "if" "(" Exp ")" Stm "else" Stm ;
-- SWhile.  Stm ::= "while" "(" Exp ")" Stm ;
SReturn. Stm ::= "return" Exp  ";" ;

terminator Stm "" ;

EAss.    Exp  ::= Ident "=" Exp ;
ELt.     Exp1 ::= Exp2 "<" Exp2 ;
ELe.     Exp1 ::= Exp2 "<=" Exp2 ;
EGt.     Exp1 ::= Exp2 ">" Exp2 ;
EGe.     Exp1 ::= Exp2 ">=" Exp2 ;
EEq.     Exp1 ::= Exp2 "==" Exp2 ;
ENe.     Exp1 ::= Exp2 "!=" Exp2 ;
EAdd.    Exp2 ::= Exp2 "+" Exp3 ;
ESub.    Exp2 ::= Exp2 "-" Exp3 ;
EMul.    Exp3 ::= Exp3 "*" Exp4 ;
EDiv.    Exp3 ::= Exp3 "/" Exp4 ;
EMod.    Exp3 ::= Exp3 "%" Exp4 ;
ENeg.    Exp4 ::= "-" Exp5 ;
ENot.    Exp4 ::= "!" Exp5 ;
ECall.   Exp5 ::= [Ident] "(" [CallArg] ")" ;
ESend.   Exp5 ::= Exp "." "send" "(" [CallArg] ")" ;
EVar.    Exp5 ::= Ident ;
EValue.  Exp5 ::= "msg" "." "value" ;
ESender. Exp5 ::= "msg" "." "sender" ;
EStr.    Exp5 ::= String ;
EInt.    Exp5 ::= Integer ;
ETrue.   Exp5 ::= "true" ;
EFalse.  Exp5 ::= "false" ;

coercions Exp 5 ;
separator CallArg "," ;
separator nonempty Ident "." ;

AExp.   CallArg ::= Exp ;
ABra.   CallArg ::= "{" "from" ":" Exp "," "value" ":" Exp "}" ;

TUInt.   Type ::= "uint" "(" Integer ")" ;
-- TRInt.   Type ::= "random_int" ;
-- TRStr.   Type ::= "random_string" ;


